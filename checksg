#!/usr/bin/gawk -f

# Cron executable to check for new signal messages and notify
# Cam Webb <https://github.com/camwebb/signal-cli-wrapper>
# This software is released under GNU GPLv3. See LICENSE file.

# Installation: 1) make this script executable
#               2) configure below
#               3) trigger via crontab, e.g.,
#                    0,10,20,30,40,50 * * * *   /home/foo/bin/checksg

# BEGIN CONFIG
@include "/home/cam/.local/share/signal-cli/scw_config.awk"
# END CONFIG

BEGIN{

  # Setup
  config()
  for (i in NUM)
    iNUM[NUM[i]] = i

  # test to see if you are online
  "ping -c 1 google.com 2> /dev/null | grep '1 received'" | getline CK
  if (!CK)
    exit 1
  
  cmd = SG " rcv"
  RS  = ""
  FS  = "\n"
  ORS = ""
  while ((cmd | getline) > 0) {
    split($4, n, " ")
    if (($1 ~ /^Envelope from/) && ($6 ~ /^Body/))
      msg = msg " " iNUM[n[2]]
  }
  msg = substr(msg, 2)
  
  if (msg) {
    system("DBUS_SESSION_BUS_ADDRESS='unix:path=/run/user/1000/bus' "   \
           "notify-send -u normal -t 36000000 'Signal msg from " msg "'")
    system("sudo /root/bin/msgterm 'Signal msg from " msg "'")
  }
}
